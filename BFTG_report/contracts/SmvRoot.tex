
\chapter{Contract SmvRoot}

\minitoc

\section{Overview}


In file {\tt SmvRoot.sol}

\section{Contract Inheritance}


\noindent\begin{tabular}{|l|p{5cm}|}\hline
Base & \\\hline
ISmvRoot & \\\hline
ISmvRootStoreCallback & \\\hline
PadawanResolver & \\\hline
ProposalResolver & \\\hline
GroupResolver & \\\hline
ProposalFactoryResolver & \\\hline
Checks & \\\hline
\end{tabular}


\section{Constant Definitions}

\begin{itemize}
\item OK
\end{itemize}


\begin{lstlisting}[firstnumber=36]
    uint8 constant CHECK_PROPOSAL = 1;
\end{lstlisting}

\begin{lstlisting}[firstnumber=37]
    uint8 constant CHECK_PADAWAN = 2;
\end{lstlisting}

\begin{lstlisting}[firstnumber=38]
    uint8 constant CHECK_GROUP = 4;
\end{lstlisting}

\begin{lstlisting}[firstnumber=39]
    uint8 constant CHECK_PROPOSAL_FACTORY = 8;
\end{lstlisting}

\begin{lstlisting}[firstnumber=40]
    uint8 constant CHECK_BFTG_ROOT_ADDRESS = 16;
\end{lstlisting}

\section{Variable Definitions}

\begin{lstlisting}[firstnumber=55]
    address _addrSmvRootStore;
\end{lstlisting}

\begin{lstlisting}[firstnumber=56]
    address _addrBftgRoot;
\end{lstlisting}

\begin{lstlisting}[firstnumber=57]
    address _addrProposalFactory;
\end{lstlisting}

\begin{lstlisting}[firstnumber=91]
    bool public _inited = false;
\end{lstlisting}

\begin{lstlisting}[firstnumber=132]
    uint32 _deployedPadawansCounter;
\end{lstlisting}

\begin{lstlisting}[firstnumber=141]
    uint32 _deployedProposalsCounter;
\end{lstlisting}

\section{Modifier Definitions}


\subsection{Modifier onlyStore}


\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=59]
    modifier onlyStore() {
        require(msg.sender == _addrSmvRootStore, Errors.ONLY_STORE);
        _;
    }
\end{lstlisting}

\section{Constructor Definitions}


\subsection{Constructor}

\begin{itemize}
\item \issueCritical{Administrative Take-over in {\tt
    SmvRoot.constructor}}{No test is performed to verify the sender
  in the case {\tt msg.sender != address(0)}. An attacker could use it
  to deploy the contract himself for another user, providing its own
  {\tt addrSmvRootStore}, i.e. with his own code for most contracts.}
\end{itemize}

\begin{lstlisting}[firstnumber=64]
    constructor(address addrSmvRootStore) public {
        if (msg.sender == address(0)) {
            require(msg.pubkey() == tvm.pubkey(), Errors.ONLY_SIGNED);
        }
        require(addrSmvRootStore != address(0), Errors.STORE_UNDEFINED);
        tvm.accept();
        
        _addrSmvRootStore = addrSmvRootStore;
        ISmvRootStore(_addrSmvRootStore).queryCode
            {value: 0.2 ton, bounce: true}
            (ContractCode.Proposal);
        ISmvRootStore(_addrSmvRootStore).queryCode
            {value: 0.2 ton, bounce: true}
            (ContractCode.Padawan);
        ISmvRootStore(_addrSmvRootStore).queryCode
            {value: 0.2 ton, bounce: true}
            (ContractCode.Group);
        ISmvRootStore(_addrSmvRootStore).queryCode
            {value: 0.2 ton, bounce: true}
            (ContractCode.ProposalFactory);
        ISmvRootStore(_addrSmvRootStore).queryAddr
            {value: 0.2 ton, bounce: true}
            (ContractAddr.BftgRoot);

        _createChecks();
    }
\end{lstlisting}

\section{Public Method Definitions}


\subsection{Function \_{}deployProposal}

\begin{itemize}
\item Minor issue: the reason to make this function public instead of
  internal is unclear.
\item Minor issue: this function should check that the code of {\tt
  Proposal} is ready before further processing.
\end{itemize}

\begin{lstlisting}[firstnumber=194]
    function _deployProposal(
        address client,
        string title,
        uint128 votePrice,
        uint128 voteTotal,
        address voteProvider,
        address group,
        address[] whiteList,
        string proposalType,
        TvmCell specific
    ) public onlyMe {
        TvmCell state = _buildProposalState(_deployedProposalsCounter);
        new Proposal {stateInit: state, value: START_BALANCE}(
            client,
            title,
            votePrice,
            voteTotal,
            voteProvider,
            group,
            whiteList,
            proposalType,
            specific,
            _codePadawan
        );
        _deployedProposalsCounter++;
    }
\end{lstlisting}

\subsection{Function deployGroup}

\begin{itemize}
\item \issueMajor{No check on {\tt SmvRoot.deployGroup}}{ No check is
  performed to verify that the group does not already exist. As a
  consequence, the use might believe it created a group with the list
  of members given in argument, whereas in fact, the group already
  existed and contains another set of members. Fix: this function
  could check for the existence using bounced messages, and use a
  callback to return the result to the caller.}
\end{itemize}

\begin{lstlisting}[firstnumber=221]
    function deployGroup(string name, address[] initialMembers) public onlyContract {
        TvmCell state = _buildGroupState(name);
        new Group
            {stateInit: state, value: START_BALANCE}
            (initialMembers);
    }
\end{lstlisting}

\subsection{Function deployPadawan}

\begin{itemize}
\item Minor issue: the function should check that the code of the
  Padawan contract was correctly initialized.
\end{itemize}

\begin{lstlisting}[firstnumber=134]
    function deployPadawan(address owner) external onlyContract {
        require(msg.value >= DEPLOY_FEE);
        require(owner != address(0));
        TvmCell state = _buildPadawanState(owner);
        new Padawan{stateInit: state, value: START_BALANCE + 2 ton}();
    }
\end{lstlisting}

\subsection{Function deployProposal}

\begin{itemize}
\item Minor issue: the function should check that the code of the
  Proposal contract was correctly initialized.
\end{itemize}

\begin{lstlisting}[firstnumber=143]
    function deployProposal(
        address client,
        string title,
        uint128 votePrice,
        uint128 voteTotal,
        address voteProvider,
        address group,
        address[] whiteList,
        string proposalType,
        TvmCell specific
    ) external override onlyContract {
        require(msg.sender == _addrProposalFactory);
        require(msg.value >= DEPLOY_PROPOSAL_FEE);
        TvmBuilder b;
        b.store(specific);
        TvmCell cellSpecific = b.toCell();
        _beforeProposalDeploy(
            client,
            title,
            votePrice,
            voteTotal,
            voteProvider,
            group,
            whiteList,
            proposalType,
            cellSpecific
        );
    }
\end{lstlisting}

\subsection{Function getStats}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=246]
    function getStats() public view returns (uint32 deployedPadawansCounter, uint32 deployedProposalsCounter) {
        deployedPadawansCounter = _deployedPadawansCounter;
        deployedProposalsCounter = _deployedProposalsCounter;
    }
\end{lstlisting}

\subsection{Function getStored}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=230]
    function getStored() public view returns (
        TvmCell codePadawan,
        TvmCell codeProposal,
        TvmCell codeGroup,
        TvmCell codeProposalFactory,
        address addrBftgRoot,
        address proposalFactory
    ) {
        codePadawan = _codePadawan;
        codeProposal = _codeProposal;
        codeGroup = _codeGroup;
        codeProposalFactory = _codeProposalFactory;
        addrBftgRoot = _addrBftgRoot;
        proposalFactory = _addrProposalFactory;
    }
\end{lstlisting}

\subsection{Function updateAddr}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=123]
    function updateAddr(ContractAddr kind, address addr) external override onlyStore {
        require(addr != address(0));
        if (kind == ContractAddr.BftgRoot) {
            _addrBftgRoot = addr;
            _passCheck(CHECK_BFTG_ROOT_ADDRESS);
        }
        _onInit();
    }
\end{lstlisting}

\subsection{Function updateCode}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=103]
    function updateCode(
        ContractCode kind,
        TvmCell code
    ) external override onlyStore {
        if (kind == ContractCode.Proposal) {
            _codeProposal = code;
            _passCheck(CHECK_PROPOSAL);
        } else if (kind == ContractCode.Padawan) {
            _codePadawan = code;
            _passCheck(CHECK_PADAWAN);
        } else if (kind == ContractCode.Group) {
            _codeGroup = code;
            _passCheck(CHECK_GROUP);
        } else if (kind == ContractCode.ProposalFactory) {
            _codeProposalFactory = code;
            _passCheck(CHECK_PROPOSAL_FACTORY);
        }
        _onInit();
    }
\end{lstlisting}

\section{Internal Method Definitions}


\subsection{Function \_{}beforeProposalDeploy}

\begin{itemize}
\item Minor issue: if there is no future use for {\tt proposal}, this
  function should be replaced by a direct call to {\tt \_deployProposal}.
\end{itemize}

\begin{lstlisting}[firstnumber=172]
    function _beforeProposalDeploy(
        address client,
        string title,
        uint128 votePrice,
        uint128 voteTotal,
        address voteProvider,
        address group,
        address[] whiteList,
        string proposalType,
        TvmCell specific
    ) private view {
        TvmCell state = _buildProposalState(_deployedProposalsCounter);
        uint256 hashState = tvm.hash(state);
        address proposal = address.makeAddrStd(0, hashState);
        // IClient(_addrDensRoot).onProposalDeploy
        //     {value: 1 ton, bounce: true}
        //     (proposal, proposalType, specific);
        this._deployProposal
            {value: 4 ton}
            (client, title, votePrice, voteTotal, voteProvider, group, whiteList, proposalType, specific);
    }
\end{lstlisting}

\subsection{Function \_{}createChecks}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=42]
    function _createChecks() private inline {
        _checkList =
            CHECK_PROPOSAL |
            CHECK_PADAWAN |
            CHECK_GROUP |
            CHECK_PROPOSAL_FACTORY |
            CHECK_BFTG_ROOT_ADDRESS;
    }
\end{lstlisting}

\subsection{Function \_{}onInit}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=93]
    function _onInit() private {
        if(_isCheckListEmpty() && !_inited) {
            _inited = true;
            TvmCell state = _buildProposalFactoryState(address(this));
            _addrProposalFactory = new ProposalFactory
                {stateInit: state, value: 0.2 ton}
                ();
        }
    }
\end{lstlisting}
