
\chapter{Contract Padawan}

\minitoc

\section{Overview}


In file {\tt Padawan.sol}

\section{Public Method Definitions}

\subsection{Function confirmVote}

\begin{itemize}
\item Minor issue (readability): an integer is used as an error. Fix:
  a constant should be defined instead.
\end{itemize}

\begin{lstlisting}[firstnumber=89]
    function confirmVote(
        uint128 votes,
        uint128 votePrice,
        address voteProvider)
    external onlyContract { votes;
        optional(ActiveProposal) optActiveProposal = _activeProposals.fetch(msg.sender);
        require(optActiveProposal.hasValue(), 111);
        uint128 activeProposalVotes = optActiveProposal.get().votes;

        address balanceProvider = voteProvider == address(0) ? voteProvider : _tokenAccounts[voteProvider];

        if(_balances[balanceProvider].locked < (activeProposalVotes) * votePrice) {
            _balances[balanceProvider].locked = (activeProposalVotes) * votePrice;
        }
        _owner.transfer(0, false, 64);
    }
\end{lstlisting}

\subsection{Function onEstimateVotes}

\begin{itemize}
\item \issueMajor{Incorrect computation in {\tt
    Padawan.onEstimateVotes}} {The value of {\tt
    \_activeProposalsLength} is wrong if the user sends his votes in
  multiple batches. Indeed, if this variable measures the size of the
  mapping {\tt \_activeProposals}, it should only be increased in the
  case {\tt !optActiveProposal.hasValue()}. Otherwise, the value is
  increased for every batch of votes, and only decreased when all
  votes have been confirmed/rejected, leading to a over-estimation of
  the number of entries in the mapping.}
\item Minor issue (readability): an integer is used as an error. Fix:
  a constant should be defined instead.
\end{itemize}

\begin{lstlisting}[firstnumber=60]
    function onEstimateVotes(
        uint128 cost,
        uint128 votePrice,
        address voteProvider,
        uint128 votes,
        bool choice)
    external override onlyContract {
        optional(ActiveProposal) optActiveProposal = _activeProposals.fetch(msg.sender);
        ActiveProposal activeProposal = optActiveProposal.hasValue() ? optActiveProposal.get() : ActiveProposal(voteProvider, votePrice, 0);
        if(!optActiveProposal.hasValue()) {
            _activeProposals[msg.sender] = activeProposal;
        }
        optional(Balance) optBalance;
        if(voteProvider == address(0)) {
            optBalance = _balances.fetch(voteProvider);
        } else {
            optional(address) optAccount = _tokenAccounts.fetch(voteProvider);
            require(optAccount.hasValue(), 115);
            optBalance = _balances.fetch(optAccount.get());
        }
        require(optBalance.hasValue(), 113);
        require(optBalance.get().total >= (activeProposal.votes * votePrice) + cost, 114);
        _activeProposals[msg.sender].votes += votes;
        _activeProposalsLength += 1;
        IProposal(msg.sender).vote
            {value: 0, flag: 64, bounce: true}
            (_owner, choice, votes);
    }
\end{lstlisting}

\subsection{Function onTokenWalletDeploy}

\begin{itemize}
\item \issueCritical{Can empty voting rights in {\tt
    Padawan.onTokenWalletDeploy}}{An attacker could send a {\tt
    onTokenWalletDeploy} message (faking to be a random root token
  contract) with as argument an existing {\tt voteProvider} of the
  user, everytime after the user called {\tt depositTokens}. As a
  result {\tt \_balances[account]} is set to 0, emptying the voting
  rights of the user for that {\tt voteProvider}. Fix: the contract
  should record the deployment requests and verify that the {\tt
    msg.sender} is one of them.}
\end{itemize}

\begin{lstlisting}[firstnumber=237]
    function onTokenWalletDeploy(address account) public {
        require(!_tokenAccounts.exists(msg.sender), Errors.INVALID_CALLER);
        _tokenAccounts[msg.sender] = account;
        _balances[account] = Balance(0, 0);
        _owner.transfer(0, false, 64);
    }
\end{lstlisting}

\subsection{Function onTokenWalletGetBalance}

\begin{itemize}
\item \issueCritical{Unbounded voting rights in {\tt
    Padawan.onTokenWalletGetBalance}}{Because the balance is added to
    the total ({\tt $+=$}), instead of replacing it, a malicious user
    could keep calling {\tt depositTokens} to keep increasing his
    total balance without sending new tokens. Fix: replace {\tt $+=$} by
    {\tt $=$ }}
\end{itemize}

\begin{lstlisting}[firstnumber=222]
    function onTokenWalletGetBalance(uint128 balance) public onlyContract {
        optional(Balance) optBalance = _balances.fetch(msg.sender);
        require(optBalance.hasValue(), Errors.NOT_AUTHORIZED_CONTRACT);
        _balances[msg.sender].total += balance;
    }
\end{lstlisting}

\subsection{Function reclaimDeposit}

\begin{itemize}
\item \issueCritical{Race condition in {\tt Padawan.reclaimDeposit}}
  {Because {\tt locked} is only increased in {\tt
      Padawan.confirmVote}, a malicious user could {\tt
      reclaimDeposit} just after {\tt Padawan.onEstimateVotes} and
    before {\tt Padawan.confirmVote}. In this case, the user can empty
    his balance, while still participating to the vote. Slashing will
    not be possible later if his vote was incorrect. Fix: locked
    amount should be recomputed for every {\tt reclaimDeposit} from
    all the active proposals.}

\item Minor issue (readability): an integer is used as an error. Fix:
  a constant should be defined instead.
\end{itemize}

\begin{lstlisting}[firstnumber=118]
    function reclaimDeposit(address voteProvider, uint128 amount, address returnTo) external onlyOwner {
        require(_reclaim.amount == 0, 130);
        require(msg.value >= QUERY_STATUS_FEE * _activeProposalsLength + 1 ton, Errors.MSG_VALUE_TOO_LOW);
        address balanceProvider = address(0);
        if(voteProvider != address(0)) {
            optional(address) optAccount = _tokenAccounts.fetch(voteProvider);
            require(optAccount.hasValue(), 117);
            balanceProvider = optAccount.get();
        }
        optional(Balance) optBalance = _balances.fetch(balanceProvider);
        require(optBalance.hasValue(), 131);
        Balance balance = optBalance.get();
        require(amount <= balance.total, Errors.NOT_ENOUGH_VOTES);
        require(returnTo != address(0), 132);

        _reclaim = Reclaim(balanceProvider, amount, returnTo);

        if (amount <= balance.total - balance.locked) {
            _doReclaim();
        }

        optional(address, ActiveProposal) optActiveProposal = _activeProposals.min();
        while (optActiveProposal.hasValue()) {
            (address addrActiveProposal,) = optActiveProposal.get();
            IProposal(addrActiveProposal).queryStatus
                {value: QUERY_STATUS_FEE, bounce: true, flag: 1}
                ();
            optActiveProposal = _activeProposals.next(addrActiveProposal);
        }
    }
\end{lstlisting}

\subsection{Function rejectVote}

\begin{itemize}
\item Minor issue (readability): an integer is used as an error. Fix:
  a constant should be defined instead.
\end{itemize}

\begin{lstlisting}[firstnumber=106]
    function rejectVote(uint128 votes, uint16 errorCode) external onlyContract { votes; errorCode;
        optional(ActiveProposal) optActiveProposal = _activeProposals.fetch(msg.sender);
        require(optActiveProposal.hasValue(), 112);
        ActiveProposal activeProposal = optActiveProposal.get();
        activeProposal.votes -= votes;
        if (activeProposal.votes == 0) {
            delete _activeProposals[msg.sender];
            _activeProposalsLength -= 1;
        }
        _owner.transfer(0, false, 64);
    }
\end{lstlisting}

\subsection{Function updateStatus}

\begin{itemize}
\item Minor issue (readability): the test for recomputation of locked
  amount should be {\tt ==} instead of {\tt <=} as the former
  locked amount can never be strictly smaller than a given proposal
  cost.
\item Minor issue (readability): the recomputation of the locked
  amount should be moved to an internal function, and reused in {\tt
    reclaimDeposit} to avoid the race condition with {\tt confirmVote}
\item Minor issue (code repetition): {\tt delete
  \_activeProposals[msg.sender]} is in both clauses of the {\tt if}
  and could be moved outside.
\item Minor issue (readability): an integer is used as an error. Fix:
  a constant should be defined instead.
\end{itemize}

\begin{lstlisting}[firstnumber=149]
    function updateStatus(ProposalState state) external onlyContract {
        optional(ActiveProposal) optActiveProposal = _activeProposals.fetch(msg.sender);
        require(optActiveProposal.hasValue());
        ActiveProposal activeProposal = optActiveProposal.get();

        if (state >= ProposalState.Ended) {
            address balanceProvider = address(0);
            if(activeProposal.voteProvider != address(0)) {
                optional(address) optAccount = _tokenAccounts.fetch(activeProposal.voteProvider);
                require(optAccount.hasValue(), 117);
                balanceProvider = optAccount.get();
            }
            Balance balance = _balances[balanceProvider];
            if(balance.locked <= activeProposal.votes * activeProposal.votePrice) {
                delete _activeProposals[msg.sender];
                uint128 max;
                optional(address, ActiveProposal) optActiveProposal2 = _activeProposals.min();
                while (optActiveProposal2.hasValue()) {
                    (address addrActiveProposal, ActiveProposal activeProposal2) = optActiveProposal2.get();
                    if(activeProposal2.votes * activeProposal2.votePrice > max && activeProposal2.voteProvider == activeProposal.voteProvider) {
                        max = activeProposal2.votes * activeProposal2.votePrice;
                    }
                    optActiveProposal2 = _activeProposals.next(addrActiveProposal);
                }
                _balances[balanceProvider].locked = max;
            } else {
                delete _activeProposals[msg.sender];
            }
            _activeProposalsLength -= 1;
            if(_reclaim.amount != 0) {
                balance = _balances[_reclaim.balanceProvider];
                if (_reclaim.amount <= balance.total - balance.locked) {
                    _doReclaim();
                }
            }
        }
    }
\end{lstlisting}
