
\chapter{Contract BftgRoot}

\minitoc

\section{Overview}

In file {\tt BftgRoot.sol}

\section{Constructor Definitions}


\subsection{Constructor}

\begin{itemize}
\item \issueCritical{Administrative Take-over in {\tt
    BftgRoot.constructor}}{No test is performed to verify the sender
  in the case {\tt msg.sender != address(0)}. An attacker could use it
  to deploy the contract himself for another user, providing its own
  {\tt addrBftgRootStore}, i.e. with his own code for most
  contracts. Fix: contract should be deployed by the same public key
  as {\tt tvm.pubkey} or the sender should be the same as a static
  variable {\tt \_deployer}.}
\item \issueMajor{No initialization check performed in {\tt
    BftgRoot.constructor}}{The {\tt \_createChecks} function gives the
  false feeling the checks are performed for initialization of the
  Padawan and Proposal codes. However, the checks are not performed in
  the functions where they would be required. No attempt is done to
  perform the same checks for addresses.}
\end{itemize}

\begin{lstlisting}[firstnumber=36]
    constructor(address addrBftgRootStore) public {
        if (msg.sender == address(0)) {
            require(msg.pubkey() == tvm.pubkey(), Errors.ONLY_SIGNED);
        }
        require(addrBftgRootStore != address(0), Errors.STORE_UNDEFINED);
        tvm.accept();

        _addrBftgRootStore = addrBftgRootStore;
        IBftgRootStore(addrBftgRootStore).queryCode
            {value: 0.2 ton, bounce: true}
            (ContractCode.Contest);
        IBftgRootStore(addrBftgRootStore).queryCode
            {value: 0.2 ton, bounce: true}
            (ContractCode.JuryGroup);

        _createChecks();
    }
\end{lstlisting}

\section{Public Method Definitions}


\subsection{OnBounce function}

\begin{itemize}
\item Minor issue: this function should check the message name being
  bounced.
\item Minor issue (readability): {\tt \_} should be avoided as a
  variable name.
\end{itemize}

\begin{lstlisting}[firstnumber=83]
    onBounce(TvmSlice) external {
        if(_juryGroupPendings.exists(msg.sender)) {
            address[] _;
            deployJuryGroup(_juryGroupPendings[msg.sender].tag, _);
            this.registerMemberJuryGroup
                {value: 0, bounce: false, flag: 64}
                (_juryGroupPendings[msg.sender].tag, _juryGroupPendings[msg.sender].addrJury);
            delete _juryGroupPendings[msg.sender];
        }
    }
\end{lstlisting}

\subsection{Function deployContest}

\begin{itemize}
\item \issueCritical{tvm.accept without check in {\tt
    BftgRoot.deployContest}}{An attacker could drain the contract
  balance by sending many messages {\tt deployContest}. Moreover, some
  of the arguments have unbounded size ({\tt tags}), providing a way
  to make the attack even more efficient by sending large message with
  high gas cost. Fix: the sender should pay the gas.}
\end{itemize}

\begin{lstlisting}[firstnumber=98]
    function deployContest(string[] tags, uint128 prizePool, uint32 underwayDuration) external view {
        tvm.accept();
        TvmCell state = _buildContestState(address(this));
        new Contest
            {stateInit: state, value: 1 ton}
            (_addrBftgRootStore, tags, prizePool, underwayDuration);
    }
\end{lstlisting}

\subsection{Function deployJuryGroup}

\begin{itemize}
\item Minor issue: a {\tt require} should check that there is enough
  value in the message to perform the deployment of the message.
\end{itemize}

\begin{lstlisting}[firstnumber=112]
    function deployJuryGroup(string tag, address[] initialMembers) public view {
        require(address(0) != msg.sender);
        TvmCell state = _buildJuryGroupState(tag, address(this));
        new JuryGroup
            {stateInit: state, value: 0.3 ton}
            (initialMembers);
    }
\end{lstlisting}

\subsection{Function getMembersCallback}

\begin{itemize}
\item Minor issue (readability): an integer is used as an error. Fix:
  a constant should be defined instead.
\item Minor issue (gas cost): the argument {\tt members} is not used
  in this function. It looks like asking for the list of members is
  only a way to check for the existence of the group. A less expensive
  function should be used instead of asking for the full list.
\end{itemize}

\begin{lstlisting}[firstnumber=130]
    function getMembersCallback(mapping(address => Member) members) public {
        require(_juryGroupPendings.exists(msg.sender) || address(this) == msg.sender, 106);
        IJuryGroup(msg.sender).registerMember
            {value: 0 ton, bounce: true, flag: 64}
            (_juryGroupPendings[msg.sender].addrJury);
        delete _juryGroupPendings[msg.sender];
    }
\end{lstlisting}

\subsection{Function registerMemberJuryGroup}

\begin{itemize}
\item \issueMajor{Non-reentrant in {\tt
    BftgRoot.registerMemberJuryGroup}} {If several {\tt
    registerMemberJuryGroup} messages are sent together for the same
  {\tt JuryGroup}, only the last one is taken into account, in {\tt
    getMembersCallback}. This issue might lead to missing members, or
  to balance problems, given that multiple messages sent to {\tt
    JuryGroup.registerMember} seems to be way to increase the balance
  for a particular member. Fix: either the contract should deal with
  multiple registration at the same time, or {\tt
    registerMemberJuryGroup} should immediately fail if a registration
  is already in progress for the same group.  }
\item Minor issue (readability): an integer is used as an error. Fix:
  a constant should be defined instead.
\end{itemize}

\begin{lstlisting}[firstnumber=120]
    function registerMemberJuryGroup(string tag, address addrMember) public override {
        address addrContest = resolveContest(address(this));
        address addrJuryGroup = resolveJuryGroup(tag, address(this));
        require(msg.sender == addrContest || address(this) == msg.sender, 105);
        _juryGroupPendings[addrJuryGroup] = JuryGroupPending(addrMember, tag);
        IJuryGroup(addrJuryGroup).getMembers
            {value: 0, bounce: true, flag: 64}
            ();
    }
\end{lstlisting}
