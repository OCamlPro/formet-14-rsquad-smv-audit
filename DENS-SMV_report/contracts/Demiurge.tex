
\chapter{Contract Demiurge}

\minitoc

In file {\tt Demiurge.sol}

The {\tt Demiurge} contract acts as a central hub to create user
contracts and proposal contracts.x

\section{Modifier Definitions}


\subsection{Modifier checksEmpty}

\begin{itemize}
\item Minor issue: this modifier is not used. It should be removed.
\end{itemize}

\begin{lstlisting}[firstnumber=66]
    modifier checksEmpty() {
        require(_allCheckPassed(), Errors.NOT_ALL_CHECKS_PASSED);
        tvm.accept();
        _;
    }
\end{lstlisting}

\section{Constructor Definitions}


\subsection{Constructor}

\begin{itemize}
\item \issueCritical{Administrative Take-over in {\tt
    Demiurge.constructor}}{No test is performed to verify the sender
  in the case {\tt msg.sender != address(0)}. An attacker could use it
  to deploy the contract himself for another user, providing its own
  {\tt addrStore}, i.e. with his own code for most contracts.}
\item \issueMajor{No initialization check performed in {\tt
    Demiurge.constructor}}{The {\tt \_createChecks} function gives the
  false feeling the checks are performed for initialization of the
  Padawan and Proposal codes. However, the checks are not performed in
  the functions where they would be required. No attempt is done to
  perform the same checks for addresses.}
\item Minor issue (readability): a number is used as an error, a
  constant should be defined instead.
\item Minor issue (duplicate code): the check {\tt addrStore $!=$
  address(0)} is performed twice, the second one is useless.
\end{itemize}

\begin{lstlisting}[firstnumber=82]
    constructor(address addrStore) public {
        if (msg.sender == address(0)) {
            require(msg.pubkey() == tvm.pubkey(), 101);
        }
        require(addrStore != address(0), Errors.STORE_SHOULD_BE_NOT_NULL);
        tvm.accept();
        
        if (addrStore != address(0)) {
            _addrStore = addrStore;
            DemiurgeStore(_addrStore).queryCode{value: 0.2 ton, bounce: true}(ContractType.Proposal);
            DemiurgeStore(_addrStore).queryCode{value: 0.2 ton, bounce: true}(ContractType.Padawan);
            DemiurgeStore(_addrStore).queryAddr{value: 0.2 ton, bounce: true}(ContractAddr.DensRoot);
            DemiurgeStore(_addrStore).queryAddr{value: 0.2 ton, bounce: true}(ContractAddr.TokenRoot);
            DemiurgeStore(_addrStore).queryAddr{value: 0.2 ton, bounce: true}(ContractAddr.Faucet);
        }

        _createChecks();
    }
\end{lstlisting}

\section{Public Method Definitions}


\subsection{Function deployPadawan}

\begin{itemize}
\item Minor issue: the function should check that the code of the
  Padawan contract was correctly initialized.
\end{itemize}

\begin{lstlisting}[firstnumber=103]
    function deployPadawan(address owner) external onlyContract {
        require(msg.value >= DEPLOY_FEE + 2 ton);
        require(owner != address(0));
        TvmCell state = _buildPadawanState(owner);
        new Padawan{stateInit: state, value: START_BALANCE + 2 ton}(_addrTokenRoot);
    }
\end{lstlisting}

\subsection{Function deployReserveProposal}

\begin{itemize}
\item Minor issue: this function should check that {\tt \_codePadawan} and {\tt \_codeProposal}
  have been correctly initialized
\item Minor issue: there is no need to store {\tt \_codePadawan} in the proposal struct as it is
  already a global variable.
\end{itemize}

\begin{lstlisting}[firstnumber=112]
    function deployReserveProposal(
        string title,
        ReserveProposalSpecific specific
    ) external onlyContract {
        require(msg.value >= DEPLOY_PROPOSAL_FEE);
        TvmBuilder b;
        b.store(specific);
        TvmCell cellSpecific = b.toCell();

        NewProposal _newProposal = NewProposal(
            0,
            _addrDensRoot,
            ProposalType.Reserve,
            cellSpecific,
            _codePadawan,
            _buildProposalState(title)
        );
        _newProposals.push(_newProposal);
        
        _beforeProposalDeploy(uint8(_newProposals.length - 1));
    }
\end{lstlisting}

\subsection{Function getTotalDistributedCb}

\begin{itemize}
\item \issueCritical{No permission check in {\tt
    Demiurge.getTotalDistributedCb}}{Anybody can send this message. An
  attacker could use it to force the deployment of all proposals with
  a wrong number of total votes.}
\item \issueCritical{No value check in {\tt
    Demiurge.getTotalDistributedCb}}{This function is in charge of
  deploying all pending proposals. It should check that the sender
  gave enough value to perform these deployments before the end of the
  action phase. Otherwise, the action phase may succeed, all proposal
  will be removed from the array of proposals, but the deployments
  will fail by lack of gas.}
\item Minor issue: this function should send back the remaining gas
  not consumed to its called, especially if the caller gave a lot of
  gas to account for the deployments of multiple proposals.
\end{itemize}

\begin{lstlisting}[firstnumber=148]
    function getTotalDistributedCb(
        uint128 totalDistributed
    ) public override {
        _totalVotes = totalDistributed;
        _getBalancePendings -= 1;
        _deployProposals();
    }
\end{lstlisting}

\subsection{Function updateAddr}

\begin{itemize}
\item Minor issue: add {\tt \_passCheck} for addresses too.
\end{itemize}

\begin{lstlisting}[firstnumber=174]
    function updateAddr(ContractAddr kind, address addr) external override onlyStore {
        require(addr != address(0));
        if (kind == ContractAddr.DensRoot) {
            _addrDensRoot = addr;
        } else if (kind == ContractAddr.TokenRoot) {
            _addrTokenRoot = addr;
        } else if (kind == ContractAddr.Faucet) {
            _addrFaucet = addr;
        }
    }
\end{lstlisting}

