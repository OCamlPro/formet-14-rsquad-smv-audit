
\chapter{Contract Proposal}

\minitoc


\section{Overview}

In file {\tt Proposal.sol}

This contract is used to collect the votes for a particular
proposal. Votes are sent by {\tt Padawan} contracts.

\section{Static Variable Definitions}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=13]
    address static _deployer;
\end{lstlisting}

\begin{lstlisting}[firstnumber=14]
    string static _title;
\end{lstlisting}

\section{Variable Definitions}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=16]
    address public _addrClient;
\end{lstlisting}

\begin{lstlisting}[firstnumber=18]
    ProposalInfo public _proposalInfo;
\end{lstlisting}

\begin{lstlisting}[firstnumber=20]
    ProposalResults _results;
\end{lstlisting}

\begin{lstlisting}[firstnumber=21]
    VoteCountModel _voteCountModel;
\end{lstlisting}

\section{Constructor Definitions}


\subsection{Constructor}

\begin{itemize}
\item Minor issue: there is a limitation to 16 kB for deploy
  messages. For this constructor, the deploy message contains the code
  of {\tt Proposal}, the title and the code of {\tt Padawan}. Thus, it
  might become a problem in the future. There is already a mechanism
  in the infrastructure to download codes from the {\tt
    DemiurgeStore}, this contract should take advantage of it.
\item Minor issue: the {\tt \_voteCountModel} variable is initialized
  to {\tt SoftMajority} in this constructor, but it is not used
  anywhere. Consider removing it if no future use.
\end{itemize}

\begin{lstlisting}[firstnumber=25]
    constructor(
        uint128 totalVotes,
        address addrClient,
        ProposalType proposalType,
        TvmCell specific,
        TvmCell codePadawan
    ) public {
        require(_deployer == msg.sender);

        _addrClient = addrClient;

        _proposalInfo.title = _title;
        _proposalInfo.start = uint32(now);
        _proposalInfo.end = uint32(now + 60 * 60 * 24 * 7);
        _proposalInfo.proposalType = proposalType;
        _proposalInfo.specific = specific;
        _proposalInfo.state = ProposalState.New;
        _proposalInfo.totalVotes = totalVotes;

        _codePadawan = codePadawan;

        _voteCountModel = VoteCountModel.SoftMajority;
    }
\end{lstlisting}

\section{Public Method Definitions}


\subsection{Function getAll}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=168]
    function getAll() public view override returns (ProposalInfo info) {
        info = _proposalInfo;
    }
\end{lstlisting}

\subsection{Function getCurrentVotes}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=181]
    function getCurrentVotes() external override view returns (uint32 votesFor, uint32 votesAgainst) {
        return (_proposalInfo.votesFor, _proposalInfo.votesAgainst);
    }
\end{lstlisting}

\subsection{Function getInfo}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=177]
    function getInfo() public view returns (ProposalInfo info) {
        info = _proposalInfo;
    }
\end{lstlisting}

\subsection{Function getVotingResults}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=172]
    function getVotingResults() public view returns (ProposalResults vr) {
        require(_proposalInfo.state > ProposalState.Ended, Errors.VOTING_HAS_NOT_ENDED);
        vr = _results;
    }
\end{lstlisting}

\subsection{Function queryStatus}

\begin{itemize}
\item Minor issue: a {\tt require} should check that the message
  contains enough value to send the message.
\end{itemize}

\begin{lstlisting}[firstnumber=162]
    function queryStatus() external override {
        IPadawan(msg.sender).updateStatus(_proposalInfo.state);
    }
\end{lstlisting}

\subsection{Function vote}

\begin{itemize}
\item Minor issue: a {\tt require} should check that the message
  contains enough value to send back the reply;
\item Minor issue: given that the constructor initializes {\tt
  \_proposalInfo.start} to {\tt now}, it is impossible for this
  function to return the {\tt VOTING\_NOT\_STARTED} error.
\item Minor issue: the transaction could be aborted if a {\tt
  onProposalPassed} message is sent by {\tt \_finalize} (in {\tt
  \_wrapUp}), together with {\tt rejectVote} or {\tt confirmVote}
  messages, because of the flag 64.  Need to test what happens if two
  messages are sent by the same transaction, with one of them
  containing the flag 64.
\end{itemize}

\begin{lstlisting}[firstnumber=55]
    function vote(address addrPadawanOwner, bool choice, uint32 votesCount) external override {
        address addrPadawan = resolvePadawan(addrPadawanOwner);
        uint16 errorCode = 0;

        if (addrPadawan != msg.sender) {
            errorCode = Errors.NOT_AUTHORIZED_CONTRACT;
        } else if (now < _proposalInfo.start) {
            errorCode = Errors.VOTING_NOT_STARTED;
        } else if (now > _proposalInfo.end) {
            errorCode = Errors.VOTING_HAS_ENDED;
        }

        if (errorCode > 0) {
            IPadawan(msg.sender).rejectVote{value: 0, flag: 64, bounce: true}(votesCount, errorCode);
        } else {
            IPadawan(msg.sender).confirmVote{value: 0, flag: 64, bounce: true}(votesCount);
            if (choice) {
                _proposalInfo.votesFor += votesCount;
            } else {
                _proposalInfo.votesAgainst += votesCount;
            }
        }

        _wrapUp();
    }
\end{lstlisting}

\subsection{Function wrapUp}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=49]
    function wrapUp() external override {
        _wrapUp();
        msg.sender.transfer(0, false, 64);
    }
\end{lstlisting}

\section{Internal Method Definitions}


\subsection{Function \_{}buildPadawanState}

\begin{itemize}
\item Minor issue (code repetition): instead of defining this
  function, the same function in {\tt PadawanResolver} should take the
  {\tt deployer} in argument.
\end{itemize}

\begin{lstlisting}[firstnumber=154]
    function _buildPadawanState(address owner) internal view override returns (TvmCell) {
        return tvm.buildStateInit({
            contr: Padawan,
            varInit: {_deployer: _deployer, _owner: owner},
            code: _codePadawan
        });
    }
\end{lstlisting}

\subsection{Function \_{}calculateVotes}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=132]
    function _calculateVotes(
        uint32 yes,
        uint32 no
    ) private view returns (bool) {
        bool passed = false;
        passed = _softMajority(yes, no);
        return passed;
    }
\end{lstlisting}

\subsection{Function \_{}changeState}

\begin{itemize}
\item OK
\end{itemize}

\begin{lstlisting}[firstnumber=150]
    function _changeState(ProposalState state) private inline {
        _proposalInfo.state = state;
    }
\end{lstlisting}

\subsection{Function \_{}finalize}

\begin{itemize}
\item Minor issue: a {\tt require} should check that the message
  contains enough value to send the {\tt onProposalPassed}
  message. This check could be moved earlier in methods calling {\tt
    \_finalize}
\end{itemize}

\begin{lstlisting}[firstnumber=81]
    function _finalize(bool passed) private {
        _results = ProposalResults(
            uint32(0),
            passed,
            _proposalInfo.votesFor,
            _proposalInfo.votesAgainst,
            _proposalInfo.totalVotes,
            _voteCountModel,
            uint32(now)
        );

        ProposalState state = passed ? ProposalState.Passed : ProposalState.NotPassed;

        _changeState(state);

        IClient(address(_addrClient)).onProposalPassed{value: 1 ton} (_proposalInfo);

        emit ProposalFinalized(_results);
    }
\end{lstlisting}

\subsection{Function \_{}softMajority}

\begin{itemize}
\item \issueCritical{Division by 0 in {\tt
    Proposal.\_softMajority}}{If {\tt totalVotes=1}, this function
  fails with division by 0. Fix: the function should check that {\tt
    totalVotes>1}, and add special cases for {\tt totalVotes=1} and
  {\tt totalVotes=0}}
\item Minor issue (readability): use {\tt returns (bool passed)} to
  avoid the need to define a temporary variable and to return it.
\end{itemize}

\begin{lstlisting}[firstnumber=141]
    function _softMajority(
        uint32 yes,
        uint32 no
    ) private view returns (bool) {
        bool passed = false;
        passed = yes >= 1 + (_proposalInfo.totalVotes / 10) + (no * ((_proposalInfo.totalVotes / 2) - (_proposalInfo.totalVotes / 10))) / (_proposalInfo.totalVotes / 2);
        return passed;
    }
\end{lstlisting}

\subsection{Function \_{}tryEarlyComplete}

\begin{itemize}
\item \issueMajor{Overflow in {\tt Proposal.\_tryEarlyComplete}}{If
  vote counts are expected to be in the full {\tt uint32} range, {\tt
    yes*2} and {\tt no*2} can overflow. Fix: use {\tt uint64} for
  parameters.}
\item Minor issue (readability): use {\tt returns (bool completed,
  bool passed)} to avoid the need to define temporary variables and to
  return them.
\end{itemize}

\begin{lstlisting}[firstnumber=101]
    function _tryEarlyComplete(
        uint32 yes,
        uint32 no
    ) private view returns (bool, bool) {
        (bool completed, bool passed) = (false, false);
        if (yes * 2 > _proposalInfo.totalVotes) {
            completed = true;
            passed = true;
        } else if(no * 2 >= _proposalInfo.totalVotes) {
            completed = true;
            passed = false;
        }
        return (completed, passed);
    }
\end{lstlisting}

\subsection{Function \_{}wrapUp}

\begin{itemize}
\item Minor issue: the function could immediately check if the state
  is above {\tt Ended} to avoid recomputing again when the state
  cannot change anymore;
\item Minor issue: there is no need to call {\tt \_changeState} before
  calling {\tt \_finalize}, as {\tt \_finalize} always calls {\tt
    \_changeState} and will thus override the state written in this
  function;
\end{itemize}

\begin{lstlisting}[firstnumber=116]
    function _wrapUp() private {
        (bool completed, bool passed) = (false, false);

        if (now > _proposalInfo.end) {
            completed = true;
            passed = _calculateVotes(_proposalInfo.votesFor, _proposalInfo.votesAgainst);
        } else {
            (completed, passed) = _tryEarlyComplete(_proposalInfo.votesFor, _proposalInfo.votesAgainst);
        }

        if (completed) {
            _changeState(ProposalState.Ended);
            _finalize(passed);
        }
    }
\end{lstlisting}
